@echo off
@cls
rem 修改下面的启动时间和关机的时间.(24小时制,只能用整数)
set /a startHour=9
set /a startMinu=00
set /a sleepHour=12+11
set /a sleepMinu=30

rem 下面开始程序,不要乱改.
rem start the program.
set regStartName=%~n0
set startPath=%~f0
call :autoRun
set /a delay = 0
set hour=%time:~0,2%
set minu=%time:~3,2%
set second=%time:~6,2%
rem test
set /a tempn=%hour%*60+%minu%
set /a temps=%startHour%*60+%startMinu%
if %tempn% GEQ %temps% (
echo OK!
echo You can play the computer until sleepTime %sleepHour%:%sleepMinu%.
echo ^^_^^
set /a delay=((%sleepHour%-%hour%^)*60+%sleepMinu%-%minu%^)*60-%second%
) else (
echo Sorry,now the time is %time:~0,-6% ,and it doesn't reach starTime %startHour%:%startMinu%.
echo So,you can't play the computer now.
echo The computer is shuting down...
set delay=30
)
echo.
echo delay=%delay% (second).
shutdown -s -t %delay%
goto :exit

:autoRun
reg query HKCU\Software\Microsoft\Windows\CurrentVersion\Run /f "%regStartName%" /e /t REG_SZ >nul
if ERRORLEVEL 1 (
reg add HKCU\Software\Microsoft\Windows\CurrentVersion\Run /v %regStartName% /t REG_SZ /d "%startPath%" /f
)
goto :eof

:exit
pause>nul